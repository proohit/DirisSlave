plugins {
    id 'java'
    id 'application'
    id 'war'
    id "com.github.ben-manes.versions" version "0.44.0"
}

application {
    mainClass = 'bot.Startup'
}

group 'com.proohit'
version '1.0.17'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
}


dependencies {
    implementation 'com.github.walkyst:lavaplayer-fork:1.3.99.2'
    implementation group: 'net.dv8tion', name: 'JDA', version: '5.0.0-alpha.18'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.jfree:jfreechart:1.0.14'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.32'
    implementation 'com.konghq:unirest-java:3.14.1'
    implementation 'com.github.mpkorstanje:simmetrics-core:4.1.1'
    implementation 'org.tinylog:tinylog-api:2.6.0'
    implementation 'org.tinylog:tinylog-impl:2.6.0'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation "org.jooq:jooq:3.15.3"
    implementation "org.jooq:jooq-meta:3.15.3"
    implementation "org.jooq:jooq-codegen:3.15.3"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '3.0.4'

}

def generatedVersionDir = "${buildDir}/generated-version"

sourceSets {
    main {
        output.dir(generatedVersionDir, builtBy: 'generateVersionProperties')
    }
}

task generateVersionProperties {
    doLast {
        def propertiesFile = file "$generatedVersionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty("version", rootProject.version.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}
processResources.dependsOn generateVersionProperties
